plugins {
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'maven-publish'
}
targetCompatibility = 1.8
sourceCompatibility = 1.8
repositories {
	mavenCentral()
	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
	maven { url = 'https://redempt.dev' }
	mavenLocal()
}
dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
	api 'com.github.Redempt:RedCommands:1.5.7'
	compileOnly 'net.kyori:adventure-api:4.17.0'
	compileOnly 'net.kyori:adventure-text-serializer-legacy:4.17.0'
}
sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'res'
		}
	}
}

jar.configure {
	actions.clear()
	dependsOn shadowJar
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	archiveClassifier.set('sources')
}

shadowJar.configure {
	archiveClassifier.set('')
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.github.Redempt'
			artifactId = rootProject.name
			version = System.env.BUILD_VERSION ?: "1.0"
			artifact javadocJar
			artifact sourcesJar
			from components.java
		}
	}
}

